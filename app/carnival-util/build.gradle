/**
 *
 * carnival-util
 *
 * Set of utility classes that help with: 
 *  - data tables
 *  - interaction with data sources
 *  - code groups
 *
 *
 */


///////////////////////////////////////////////////////////////////////////////
// build script settings
///////////////////////////////////////////////////////////////////////////////
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.21'
        classpath group: 'org.kordamp.gradle', name: 'stats-gradle-plugin', version: '0.2.2'
    }    
}


///////////////////////////////////////////////////////////////////////////////
// plugins
///////////////////////////////////////////////////////////////////////////////
plugins {
    id 'groovy'
    id 'maven-publish'
}
apply plugin: 'org.kordamp.gradle.stats'


///////////////////////////////////////////////////////////////////////////////
// maven publishing
///////////////////////////////////////////////////////////////////////////////
publishToMavenLocal.dependsOn 'compileGroovy'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    from sourceSets.main.allGroovy
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    from groovydoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenGroovy(MavenPublication) {
            artifactId = 'carnival-util'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'Carnival Util'
                    description 'Utility classes and services in the Carnival Framework.'
                    url 'https://github.com/pmbb-ibi/carnival'
                    licenses {
                        license {
                            name 'GNU GENERAL PUBLIC LICENSE, Version 3, 29 June 2007'
                            url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }
                    developers {
						developer {
                            id 'augustearth'
                            name 'David Birtwell'
                            email 'augustearth@gmail.com'
                        }
                        developer {
                            id 'hjwilli'
                            name 'Heather Williams'
                            email 'hjwilli@gmail.com'
                        }
                    }
                    scm {
                        connection 'scm:git:git://github.com:pmbb-ibi/carnival.git'
                        developerConnection 'scm:git:ssh://github.com:pmbb-ibi/carnival.git'
                        url 'https://github.com/pmbb-ibi/carnival'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}



///////////////////////////////////////////////////////////////////////////////
// dependencies
///////////////////////////////////////////////////////////////////////////////

repositories {
    maven { url "https://repo1.maven.org/maven2" }
    maven { url "https://jitpack.io" }
    maven { url "http://download.java.net/maven/2/" }
    maven { url "http://central.maven.org/maven2"}

    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}


configurations {
    providedRuntime
    apt
}


// In this section you declare the dependencies for your production and test code
dependencies {

    // Groovy
    //compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.8', ext: 'pom'
    compile group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.8'
    compile group: 'org.codehaus.groovy', name: 'groovy-macro', version: '2.5.8'
    compile group: 'org.codehaus.groovy', name: 'groovy-ant', version: '2.5.8'
    compile group: 'org.codehaus.groovy', name: 'groovy-sql', version: '2.5.8'
    compile group: 'org.codehaus.groovy', name: 'groovy-dateutil', version: '2.5.8'
    compile group: 'org.codehaus.groovy', name: 'groovy-json', version: '2.5.8'
    compile group: 'org.codehaus.groovy', name: 'groovy-templates', version: '2.5.8'

    //compile group: 'net.sf.opencsvopencsv', name: 'opencsv', version: '2.3'
    compile 'com.xlson.groovycsv:groovycsv:1.3'

    // https://mvnrepository.com/artifact/au.com.bytecode/opencsv
    compile group: 'au.com.bytecode', name: 'opencsv', version: '2.4'

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.23'
    
    // We use the awesome Spock testing and specification framework
    testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'

    // GPars for parrallel computing
    compile 'org.codehaus.gpars:gpars:1.2.1'

    // Apache commons
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    // https://mvnrepository.com/artifact/org.apache.poi/poi-ooxml
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
}


compileGroovy {
    options.annotationProcessorPath = configurations.apt
}








///////////////////////////////////////////////////////////////////////////////
// BUILD 
///////////////////////////////////////////////////////////////////////////////
task createProperties(dependsOn: processResources) {
  doLast {
    new File("$buildDir/resources/main/version.properties").withWriter { w ->
        Properties p = new Properties()
        p['version'] = project.version.toString()
        p.store w, null
    }
  }
}

classes {
    dependsOn createProperties
}



